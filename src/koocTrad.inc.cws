#include "patchLib/cnormPatchLib.inc.cws"

function	koocTradgetVars(theBlock : node, theModule : node, theVar : value, decored_vars : reference)
{
  local curstart = "";
  local curname;
  local moduleName;
  if (existVariable(theModule.ctype.identifier))
    {
      moduleName = theModule.ctype.identifier;
      curstart = "M";
    }
  else
    moduleName = theModule;
  curstart = curstart + "_" + lengthString(moduleName) + moduleName + "_variable_";
  curname = "_" + lengthString(theVar) + theVar;
  if (existVariable(theModule.ctype.identifier))
    {
      foreach type in theBlock.types
      {
	if (moduleName == key(type))
	  foreach var in type.ctype.list
	  {
	    if ((leftString(var.name, lengthString(curstart)) == curstart) && (rightString(var.name, lengthString(curname)) == curname))
	      insert decored_vars[var.name] = var.name;
	  }
      }
    }
  else
    {
      foreach var in theBlock.variables
      {
	if ((leftString(var.name, lengthString(curstart)) == curstart) && (rightString(var.name, lengthString(curname)) == curname))
	  insert decored_vars[var.name] = var.name;
      }
    }
}

function	koocTradTestVarType(theName : value, theCType : reference)
{
  local strList;
  cutString(theName, "_", strList);
  local i = 3;
  local j = 0;
  if (strList[i] != "signed" && strList[i] != "unsigned")
    insert theCType.identifier = strList[i];
  else
    {
      i = add(i, 1);
      insert theCType.identifier = strList[i];
    }
  i = add(i, 1);
  while (i < getArraySize(strList))
    {
      if (strList[i] == "point")
	{
	  insert theCType.pointer[j].level = "*";
	  j = add(j, 1);
	}
      i = add(i, 1);
    }
  return (theCType);
}

function	koocTradVar(theAst : node, theBlock : node, init : node, theType : node)
{
  local decored_vars;
  local theCType;
  local module = init.module;
  
  foreach var in theBlock.variables
  {
    if (key(var) == init.module)
      setall module = var;
  }
  foreach var in theBlock#parent.list
  {
    if (var.name == init.module)
      setall module = var;
  }
  koocTradgetVars(theAst, module, init.value, decored_vars);
  foreach var in decored_vars
  {
    clearVariable(theCType);
    koocTradTestVarType(var, theCType);
    if (theCType.identifier == theType.identifier)
      {
	if (existVariable(theType.pointer) && existVariable(theCType.pointer))
	  {
	    if (equalTrees(theCType.pointer, theType.pointer))
	      return var;
	  }
	else if (!existVariable(theType.pointer))
	  return var;
      }
  }
  error(init.value + " of type " + theCType.identifier + " has never been declared in " + init.module);
}

function	koocTradClassCall(init : node)
{
  local tmp;

  setall tmp = init;
  insert init.right;
  setall init.right = tmp;
  insert init.left;
  setall init.left = tmp;
  init.left.value = init.module;
  init.right.value = init.value;
  removeVariable(init.value);
  if (existVariable(init.__LINE__))
    {
      removeVariable(init.right.__LINE__);
      removeVariable(init.__LINE__);
    }
  init.type = "deref_expression";
  init.otype = "likebinary";
  init.operator = "->";
}

function	koocTradgetType(theBlock : node, init : node, theVar : node, theNode : node, theAst : node)
{
  if (existVariable(init.ctype))
    {
      if (existVariable(init.conflict))
	{
	  init.value = koocTradVar(theAst, theBlock, init, init.ctype);
	  foreach var in theBlock.variables
	  {
	    if (key(var) == init.module)
	      koocTradClassCall(init);
	  }
	  foreach var in theBlock#parent.list
	  {
	    if (var.name == init.module)
	      koocTradClassCall(init);
	  }
	}
      insert theNode.ctype = init.ctype;
      ref theNode.ctype = init.ctype;
      return ;
    }
  if (existVariable(theVar.ctype))
    {
      if (existVariable(init.conflict))
	{
	  init.value = koocTradVar(theAst, theBlock, init, theVar.ctype);
	  foreach var in theBlock.variables
	  {
	    if (key(var) == init.module)
	      koocTradClassCall(init);
	  }
	  foreach var in theBlock#parent.list
	  {
	    if (var.name == init.module)
	      koocTradClassCall(init);
	  }
	}
      insert theNode.ctype = theVar.ctype;
      ref theNode.ctype = theVar.ctype;
      return ;
    }
  foreach var in theBlock.variables
  {
    if (key(var) == theVar.value)
      {
	if (existVariable(init.conflict))
	  {
	    init.value = koocTradVar(theAst, theBlock, init, var.ctype);
	    foreach var in theBlock.variables
	    {
	      if (key(var) == init.module)
		koocTradClassCall(init);
	    }
	    foreach var in theBlock#parent.list
	    {
	      if (var.name == init.module)
		koocTradClassCall(init);
	    }
	  }
	insert theNode.ctype = var.ctype;
	ref theNode.ctype = var.ctype;
	return ;
      }
  }
  if (existVariable(theNode.ctype))
    {
      if (existVariable(init.conflict))
	{
	  init.value = koocTradVar(theAst, theBlock, init, theNode.ctype);
	  foreach var in theBlock.variables
	  {
	    if (key(var) == init.module)
	      koocTradClassCall(init);
	  }
	  foreach var in theBlock#parent.list
	  {
	    if (var.name == init.module)
	      koocTradClassCall(init);
	  }
	}
      return ;
    }
}

function	koocTradRecScan(Curr : node, theBlock : node, theAst : node)
{
  if (existVariable(Curr.right))
    {
      koocTradRecScan(Curr.right, theBlock, theAst);
      if (existVariable(Curr.right.ctype))
	{
	  insert Curr.ctype = Curr.right.ctype;
	  ref Curr.ctype = Curr.right.ctype;
	}
    }
  if (existVariable(Curr.left))
    {
      koocTradRecScan(Curr.left, theBlock, theAst);
      if (existVariable(Curr.left.ctype))
	{
	  insert Curr.ctype = Curr.left.ctype;
	  ref Curr.ctype = Curr.left.ctype;
	}
    }
  if (existVariable(Curr.right.conflict))
    {
      koocTradgetType(theBlock, Curr.right, Curr.left, Curr, theAst);
      removeVariable(Curr.right.module);
      removeVariable(Curr.right.conflict);
    }
  if (existVariable(Curr.left.conflict))
    {
      koocTradgetType(theBlock, Curr.left, Curr.right, Curr, theAst);
      removeVariable(Curr.left.module);
      removeVariable(Curr.left.conflict);
    }
  if (existVariable(Curr.list))
    koocTradRecScan(Curr.list, theBlock, theAst);
  if (existVariable(Curr.conflict) && existVariable(Curr.ctype))
    {
      koocTradgetType(theBlock, Curr, Curr, Curr, theAst);
      removeVariable(Curr.module);
      removeVariable(Curr.conflict);
    }
  foreach var in Curr
  {
    if (existVariable(var.block))
      koocTradRecScan(var.block, var.block, theAst);
    if (existVariable(var.list))
      koocTradRecScan(var.list, theBlock, theAst);
    if (existVariable(var.condition))
      koocTradRecScan(var.condition, theBlock, theAst);
    if (existVariable(var.case))
      koocTradRecScan(var.case, theBlock, theAst);
    if (existVariable(var.init))
      koocTradRecScan(var.init, theBlock, theAst);
    if (existVariable(var.increment))
      koocTradRecScan(var.increment, theBlock, theAst);
    if (existVariable(var.stmt))
      koocTradRecScan(var.stmt, theBlock, theAst);
    if (existVariable(var.else))
      koocTradRecScan(var.else, theBlock, theAst);
    if (existVariable(var.then))
      koocTradRecScan(var.then, theBlock, theAst);
    if (existVariable(var.right))
      koocTradRecScan(var.right, theBlock, theAst);
    if (existVariable(var.left))
      koocTradRecScan(var.left, theBlock, theAst);
    if (existVariable(var.right.conflict))
      {
	koocTradgetType(theBlock, var.right, var.left, var, theAst);
	removeVariable(var.right.module);
	removeVariable(var.right.conflict);
      }
    if (existVariable(var.left.conflict))
      {
	koocTradgetType(theBlock, var.left, var.right, var, theAst);
	removeVariable(var.left.module);
	removeVariable(var.left.conflict);
      }
    if (existVariable(var.conflict) && existVariable(var.ctype))
      {
    	koocTradgetType(theBlock, var, var, var, theAst);
    	removeVariable(var.module);
    	removeVariable(var.conflict);
      }
  }
}

function	koocTradFormType(theCType : node, thePoint : value)
{
  local i = 0;
  while (i < lengthString(thePoint))
    {
      pushItem theCType.pointer;
      insert theCType.pointer#back.level= "*";
      i = add(i, 1);
    }
}

function	koocTradnewFunc(theBlock : node, theDecl : value)
{
  local newCode;

  cnormPatchInit(newCode);
  cnormPatchStatement(newCode, theDecl);
  setall theBlock = newCode.block[0];
}

function	koocTradAddParam(theBlock : node, type : value, param : value)
{
  local newCode;

  cnormPatchInit(newCode);
  cnormPatchStatement(newCode, "f(" + param + ");");

  pushItem theBlock;
  setall theBlock#back = newCode.block[0].list[0];
}

function	koocTradDecorFunc(theBlock : node, theTypes : node, theModule : value , theName : value)
{
  local ParsedType;
  local i;
  local tmp;

  theBlock.left.value = "_" + lengthString(theModule) + theModule + "_function_";
  theBlock.left.value = theBlock.left.value;
  foreach type in theTypes
  {
    i = 0;
    pushItem ParsedType;
    cutString(type, "_", tmp);
    if (tmp[0] == "int" || tmp[0] == "char" || tmp[0] == "short" || tmp[0] == "long" || tmp[0] == "longlong")
      ParsedType#back = ParsedType#back + "signed_";
    ParsedType#back = ParsedType#back + tmp[0];
    while (i < lengthString(tmp[1]))
      {
	ParsedType#back = ParsedType#back + "_point";
	i = add(i, 1);
      }
  }
  theBlock.left.value = theBlock.left.value + ParsedType[0] + "_" + lengthString(theName) + theName + "_param";
  i = 1;  
  while (i < getArraySize(ParsedType))
    {
      theBlock.left.value = theBlock.left.value + "_" + ParsedType[i];
      i = add(i, 1);
    }
}
